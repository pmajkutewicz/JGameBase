apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply from: 'build-release.gradle'

group = 'jgamebase'
version = '0.64.7-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

def releaseBuildDir = "$rootDir/build/output"
def mainClass = 'jgamebase.JGameBase'

repositories {
    maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'antlr', name: 'antlr', version: '2.7.7'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.2'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.1.1'
    compile group: 'org.apache.derby', name: 'derby', version: '10.13.1.1'
    compile group: 'dom4j', name: 'dom4j', version: '1.6.1'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '3.6.7.Final'
    compile group: 'org.hibernate.javax.persistence', name: 'hibernate-jpa-2.0-api', version: '1.0.1.Final'
    compile group: 'com.healthmarketscience.jackcess', name: 'jackcess', version: '1.2.4'
    compile group: 'javassist', name: 'javassist', version: '3.12.1.GA'
    compile group: 'javax.transaction', name: 'jta', version: '1.1'
    compile group: 'log4j', name: 'log4j', version: '1.2.17'
    compile group: 'net.sf.opencsv', name: 'opencsv', version: '2.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.24'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.24'
    compile group: 'org.swinglabs', name: 'swingx', version: '1.6.1'
    compile group: 'org.swinglabs.swingx', name: 'swingx-beaninfo', version: '1.6.5-1'
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4-rc-3'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.8'
    testCompile group: 'cglib', name: 'cglib-nodep', version: '3.2.4'
    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.5.1'
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/java"]
            includes = ["**/*.xml"]
        }
    }
}

// plugins configuration

jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}

tasks.withType(FindBugs) {
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'high'
    reports {
        xml.enabled true
        html.enabled false
    }
}

test {
    maxParallelForks = 10
    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

jar {
    archiveName = 'jgamebase.jar'
    destinationDir = file("$releaseBuildDir")
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { 'lib/' + it.getName() }.join(' '),
                'Main-Class': mainClass
        )
    }
}

// Custom tasks 

task copyDependencies(type: Copy) {
    from configurations.compile
    into "$releaseBuildDir/lib"
}

// tasks dependencies
build.dependsOn copyDependencies
check.dependsOn jacocoTestReport
